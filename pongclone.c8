;; Pong Clone
;; @author Taylor Ritenour

;; V2 - Vertical Location of paddle1
;; V3 - Vertical location of paddle2
;; V4 - X location of ball
;; V5 - Y location of ball
;; V6 - Direction vector of ball 0: NW, 1: NE, 2: SE, 3: SW
;; V7 - Previous Y location of paddle1
;; V8 - Previous Y location of paddle2
;; V9 - Previous X location of ball
;; VA - Previous Y location of ball
;; VB - 1 if anybody scores

JP MAIN
BALL:
	DW #8000
PADDLE:
	DW #8080
	DW #8080
	DW #8080
BALLNW:
	LD V0, 1
	SUB V4, V0
	SUB V5, V0
	JP ENDBALL
BALLNE:
	LD V0, 1
	ADD V4, 1
	SUB V5, V0
	JP ENDBALL
BALLSE:
	ADD V4, 1
	ADD V5, 1
	JP ENDBALL
BALLSW:
	LD V0, 1
	SUB V4, V0
	ADD V5, 1
	JP ENDBALL
NORTHDIRCHANGE:
	SE V6, 0
	JP SETNW2
	JP SETNW3
SOUTHDIRCHANGE:
	SE V6, 3
	JP SETNW1
	JP SETNW0
WESTDIRCHANGE:
	SE V6, 0
	JP SETNW2
	JP SETNW1
EASTDIRCHANGE:
	SE V6, 1
	JP SETNW3
	JP SETNW0
SETNW0:
	LD V6, 0
	RET
SETNW1:
	LD V6, 1
	RET
SETNW2:
	LD V6, 2
	RET
SETNW3:
	LD V6, 3
	RET
;; Manage the Ball
MANAGEBALL:
	LD V9, V4
	LD VA, V5
	SNE V5, 0
	CALL NORTHDIRCHANGE
	SNE V5, 31
	CALL SOUTHDIRCHANGE
MIDDLEBALL:
	SNE V6, 0
	JP BALLNW
	SNE V6, 1
	JP BALLNE
	SNE V6, 2
	JP BALLSE
	SNE V6, 3
	JP BALLSW
ENDBALL:
	SE V4, V9
	CALL UPDATEBALL
	SE VF, 1
	CALL SCORE
	LD VF, 0
	SNE V4, 0
	CALL WESTDIRCHANGE
	SNE V4, 61
	CALL EASTDIRCHANGE
	RET
SCORE:
	LD VB, 0
	SNE V4, 0
	LD VB, 1
	SNE V4, 61
	LD VB, 1
	RET
MANAGEPADDLE1:
	LD V7, V2
	LD V1, 0
	SKNP V1
	JP MOVEPAD1UP
	LD V1, 1
	SKNP V1
	JP MOVEPAD1DOWN
	RET
MOVEPAD1UP:
	LD V0, 1
	SE V2, 0
	SUB V2, V0
	CALL UPDATEPADDLE1
	RET
MOVEPAD1DOWN:
	SE V2, 26
	ADD V2, 1
	CALL UPDATEPADDLE1
	RET
MANAGEPADDLE2:
	LD V8, V3
	LD V1, 2
	SKNP V1
	JP MOVEPAD2UP
	LD V1, 3
	SKNP V1
	JP MOVEPAD2DOWN
	RET
MOVEPAD2UP:
	LD V0, 1
	SE V3, 0
	SUB V3, V0
	CALL UPDATEPADDLE2
	RET
MOVEPAD2DOWN:
	SE V3, 26
	ADD V3, 1
	CALL UPDATEPADDLE2
	RET
;; Initialize Game State
INIT:
	LD V2, 14
	LD V3, 14
	LD V4, 30
	LD V5, 14
	LD V6, 0
	RET
UPDATEBALL:
	LD I, BALL
	DRW V9, VA, 1
	LD VF, 0
	DRW V4, V5, 1
	RET
UPDATEPADDLE1:
	LD I, PADDLE
	LD V0, 0
	DRW V0, V7, 6
	DRW V0, V2, 6
	RET
UPDATEPADDLE2:
	LD I, PADDLE
	LD V0, 61
	DRW V0, V8, 6
	DRW V0, V3, 6
	RET
;; Draw everything in its position
DRAWINIT:
	LD I, BALL
	DRW V4, V5, 1
	LD I, PADDLE
	LD V0, 0
	DRW V0, V2, 6
	LD V0, 61r
	DRW V0, V2, 6
	RET
RESET:
	CLS
	CALL INIT
	CALL DRAWINIT
	JP GAMELOOP
GAMELOOP:
	LD V0, 1
	LD DT, V0
	CALL MANAGEBALL
	SNE VB, 1
	JP RESET
	CALL MANAGEPADDLE1
	CALL MANAGEPADDLE2
	LD V0, 4
	SKNP V0
	JP MAIN
	JP WAIT
WAIT:
	LD V0, DT
	SE V0, 0
	JP WAIT
	JP GAMELOOP
P:
	DW #E090
	DW #E080
	DW #8000
O:
	DW #6090
	DW #9090
	DW #6000
N:
	DW #90D0
	DW #B090
	DW #9000
G:
	DW #7080
	DW #B090
	DW #6000
EXCLAM:
	DW #8080
	DW #8000
	DW #8000
R:
	DW #E090
	DW #E090
	DW #9000
E:
	DW #F080
	DW #F080
	DW #F000
S:	
	DW #F080
	DW #F010
	DW #F000
A:
	DW #F090
	DW #F090
	DW #9000
Y:
	DW #A0A0
	DW #4040
	DW #4000
K:
	DW #90A0
	DW #C0A0
	DW #9000
;; Main method
MAIN:
	CLS
	LD V0, 20
	LD V1, 5
	LD I, P
	DRW V0, V1, 5
	ADD V0, 5
	LD I, O
	DRW V0, V1, 5
	ADD V0, 5
	LD I, N
	DRW V0, V1, 5
	ADD V0, 5
	LD I, G
	DRW V0, V1, 5
	ADD V0, 5
	LD I, EXCLAM
	DRW V0, V1, 5

	LD V0, 2
	LD V1, 18
	LD I, P
	DRW V0, V1, 5
	ADD V0, 5
	LD I, R
	DRW V0, V1, 5
	ADD V0, 5
	LD I, E
	DRW V0, V1, 5
	ADD V0, 5
	LD I, S
	DRW V0, V1, 5
	ADD V0, 5
	DRW V0, V1, 5
	ADD V0, 8
	LD I, A
	DRW V0, V1, 5
	ADD V0, 5
	LD I, N
	DRW V0, V1, 5
	ADD V0, 5
	LD I, Y
	DRW V0, V1, 5
	ADD V0, 7
	LD I, K
	DRW V0, V1, 5
	ADD V0, 5
	LD I, E
	DRW V0, V1, 5
	ADD V0, 5
	LD I, Y
	DRW V0, V1, 5
	ADD V0, 5
	
	LD V0, 25
	LD DT, V0
HANG:
	LD V0, 0
	SKNP V0
	JP RESET
	LD V0, 1
	SKNP V0
	JP RESET
	LD V0, 2
	SKNP V0
	JP RESET
	LD V0, 3
	SKNP V0
	JP RESET
	JP HANG
